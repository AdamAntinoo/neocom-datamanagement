apply plugin: 'java-library'
apply plugin: 'de.undercouch.download'
apply plugin: 'org.hidetake.swagger.generator'

archivesBaseName = 'ESI.SwaggerApi'
version = '2.0.0'
group = 'org.dimensinfin.eveonline.neocom'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }

    dependencies {
        classpath "de.undercouch:gradle-download-task:3.3.0"
        classpath "gradle.plugin.org.hidetake:gradle-swagger-generator-plugin:2.4.2"
    }
}

def swaggerGenerated = "$projectDir/generated/"

sourceSets {
    main {
        java {
            srcDirs += "$swaggerGenerated/src/main/java"
        }
    }
    test {
        java {
            srcDirs += "$swaggerGenerated/src/test/java"
        }
    }
}

dependencies {
    // P R O J E C T    D E P E N D E N C I E S

    // L O C A L    L I B R A R I E S

    // M O D U L E    D E P E N D E N C I E S
    // E S I S W A G G E R

    // P L A T F O R M    D E P E N D E N C I E S
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.1'
    compileOnly 'io.swagger:swagger-annotations:1.5.17'
    compileOnly 'joda-time:joda-time:2.9.9'
    compileOnly 'com.squareup.retrofit2:retrofit:2.3.0'
    compileOnly 'com.squareup.retrofit2:converter-gson:2.3.0'
    compileOnly 'com.squareup.retrofit2:converter-scalars:2.3.0'
    compileOnly 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1'
// https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.4'

    // T E S T I N G
    testImplementation 'junit:junit:4.12'
}

// --- T A S K S -----
import de.undercouch.gradle.tasks.download.Download

task swaggerDownload(type: Download) {
    src 'https://esi.tech.ccp.is/latest/swagger.json'
    dest projectDir
    acceptAnyCertificate true
    onlyIfNewer true
    compress false
}

//    task swaggerGenerate(type: GenerateSwaggerCode, dependsOn: swaggerDownload) {
task swaggerGenerate(type: GenerateSwaggerCode) {
    language = 'java'

    inputFile = file(new File(projectDir, 'swagger.json'))
    outputDir = file(swaggerGenerated)
    configFile = file('swagger-config.json')
}

task swaggerBuild(type:Delete, dependsOn: swaggerGenerate) {
    delete "$swaggerGenerated/docs"
    delete "$swaggerGenerated/gradle"
    delete "$swaggerGenerated/src/test/"
    delete "$swaggerGenerated/src/main/AndroidManifest.xml"
    // Remove the client code because it is not used.
    delete "$swaggerGenerated/src/main/java/org/dimansinfin/eveonline/neocom/esiswagger/client"
    delete fileTree("$swaggerGenerated").include('*')
}

task swaggerDelete(type: Delete) {
    delete "$swaggerGenerated"
}

compileJava.dependsOn swaggerBuild

clean.doFirst {
    delete "$swaggerGenerated"
}
