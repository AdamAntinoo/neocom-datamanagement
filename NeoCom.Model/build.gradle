//--- P L U G I N S
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

//--- P R O J E C T   D E F I N I T I O N
group = 'org.dimensinfin'
version = '0.14.0'
archivesBaseName = 'NeoCom.Model'
description = 'Version with XML api stripped out because deprecation. New rework where production files should be moved to ' +
        'Infinity. Swagger version 0.7.5'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/java-core'
            srcDirs += 'src/main/java-model'
            srcDirs += 'src/main/java-database'
            srcDirs += 'src/main/java-market'
            srcDirs += 'src/main/java-infinity'
            srcDirs += 'src/main/java-planetary'
            srcDirs += 'src/main/java-industry'
        }
    }
}

//--- T A S K S
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

//--- P U B L I S H I N G
publishing {
    publications {
        jar(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact(sourcesJar)
            artifact(javadocJar)
            artifact("$buildDir/libs/${archivesBaseName}-${version}.jar")
        }
    }
    repositories {
        mavenLocal()
        maven {
            url "$buildDir/repo"
        }
        maven {
            url "production"
        }
    }
}

//--- P R O D U C T   V E R S I O N S
ext {
    jacksonVersion = '2.8.6'
    ormliteVersion = '4.48'
}

//--- D E P E N D E N C I E S
dependencies {
    // M O D U L E    D E P E N D E N C I E S
    // C O R E   P L A T F O R M
    compileOnly 'org.slf4j:slf4j-api:1.7.25'
    implementation 'joda-time:joda-time:2.3'

    // P R O J E C T S
//    compileOnly project(path: ':ESI.SwaggerApi')

    // L O C A L   L I B R A R I E S
    implementation files('libs/jeveassets.3.1.2.jar')

    // N E O C O M . M O D E L
    compileOnly 'org.dimensinfin:CoreBase.core:2.2.0'
    compileOnly 'org.dimensinfin:ESI.SwaggerApi:3.0.0'

    implementation 'com.beimin:eveapi:6.0.6'
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-json-org:$jacksonVersion"
    implementation 'org.modelmapper:modelmapper:1.1.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.annimon:stream:1.1.9'

    // D A T A B A S E
    implementation "com.j256.ormlite:ormlite-core:$ormliteVersion"
    implementation 'javax.persistence:javax.persistence-api:2.2'

    // T E S T I N G   D E P E N D E N C I E S
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.slf4j:slf4j-simple:1.7.25'
    testImplementation 'joda-time:joda-time:2.3'
}
